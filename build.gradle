buildscript {
    repositories {
        mavenCentral();
        jcenter();
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
    }
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'

    repositories {
        mavenCentral();
        mavenLocal();
        jcenter();
        maven {
            name = "bintray"
            url = "http://jcenter.bintray.com"
        }
        maven {
            name = "Sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        testCompile 'junit:junit:4.12'
    }
    group = 'me.mrkirby153'
    version = '2.0-SNAPSHOT'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    publishing {
        tasks.publish.dependsOn 'build'
        repositories {
            maven {
                name = "mrkirby153"
                if(project.version.endsWith("-SNAPSHOT")) {
                    url = project.snapshotUrl
                } else {
                    url = project.releaseUrl
                }
                credentials {
                    username project.repositoryUsername
                    password project.repositoryPassword
                }
            }
        }
    }
    dependencies{
        compile 'com.google.code.gson:gson:2.6.2'
    }
}

apply plugin: 'com.github.johnrengelman.shadow'

jar {
    manifest {
        attributes 'Main-Class': 'me.mrkirby153.uhc.bot.Main'
    }
}

dependencies {
    compile project(':KC-UHC-Bot-Network')
    compile 'net.dv8tion:JDA:2.2.1_353'
    compile 'jline:jline:2.13'
    compile 'org.apache.logging.log4j:log4j-core:2.5'
    compile 'com.google.guava:guava-io:r03'
}
build.dependsOn 'shadowJar'
publishing {
    publications {
        shadow(MavenPublication) {
            from components.shadow
        }
    }
}
shadowJar {
    classifier = ''
    configurations = [project.configurations.compile]
}